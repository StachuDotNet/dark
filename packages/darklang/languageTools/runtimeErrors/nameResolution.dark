module Darklang =
  module LanguageTools =
    module RuntimeErrors =
      module NameResolution =
        // <aliases>
        type ES =
          PACKAGE.Darklang.LanguageTools.RuntimeErrors.ErrorSegment.ErrorSegment
        // </aliases>

        type ErrorType =
          | NotFound
          | ExpectedEnumButNot
          | ExpectedRecordButNot
          | MissingEnumModuleName of caseName: String
          | InvalidPackageName

        type NameType =
          | Function
          | Type
          | Constant

        type Error =
          {
            errorType: ErrorType

            nameType: NameType

            /// The `.`-delimited name _parts_
            /// e.g. `Builtin.List.fakeFunction` is `["Builtin"; "List"; "fakeFunction"]`
            names: List<String>
          }

        let toSegments (e: Error) : ErrorOutput =
          match e.errorType with
          | NotFound ->
            let nameType =
              match e.nameType with
              | Function -> "function"
              | Type -> "type"
              | Constant -> "constant"

            ErrorOutput
              { summary =
                  [ ES.String $"There is no {nameType} named "
                    ES.InlineVarName(Stdlib.String.join e.names ".") ]
                extraExplanation = []
                actual = []
                expected = [] }

          | MissingEnumModuleName caseName ->
            let name =
              let names = Builtin.List.append e.names [ caseName ]
              ES.VarName(Builtin.String.join names ".")

            let summary =
              [ ES.String "Missing type name for enum case"
                ES.String ": "
                ES.String caseName ]

            ErrorOutput
              { summary = summary
                extraExplanation = []
                actual = [ name ]
                expected = [] }

          | InvalidPackageName ->
            let summary =
              [ ES.String "Invalid package name "
                ES.InlineVarName(Stdlib.String.join e.names ".") ]

            ErrorOutput
              { summary = summary
                extraExplanation = []
                actual = []
                expected = [] }

          | ExpectedRecordButNot ->
            let summary =
              [ ES.String "Expected a record but "
                ES.InlineVarName(Stdlib.String.join e.names ".")
                ES.String " is an enum" ]

            ErrorOutput
              { summary = summary
                extraExplanation = []
                actual = []
                expected = [] }

          | _ ->
            // RTETODO:
            // | ExpectedEnumButNot
            ErrorOutput
              { summary = [ ES.String "RTETODO NameResolution.toSegments" ]
                extraExplanation = []
                actual = []
                expected = [] }