module Darklang =
  module LanguageTools =
    module RuntimeErrors =
      // <aliases>
      type Option = Stdlib.Option.Option
      type ID = LanguageTools.ID
      type TLID = LanguageTools.TLID

      // language
      type RTTypeReference = LanguageTools.RuntimeTypes.TypeReference

      type RTDval = LanguageTools.RuntimeTypes.Dval.Dval

      type RTTypeName = LanguageTools.RuntimeTypes.FQTypeName.FQTypeName

      type RTFnName = LanguageTools.RuntimeTypes.FQFnName.FQFnName
      type RTParam = LanguageTools.RuntimeTypes.Param

      // RuntimeErrors
      type REErrorOutput = LanguageTools.RuntimeErrors.ErrorOutput
      // </aliases>


      // CLEANUP consider renaming this module to RuntimeTypeChecker
      module TypeChecker =
        type Context =
          | FunctionCallParameter of
            fnName: RTFnName *
            parameter: RTParam *
            paramIndex: Int64

          | FunctionCallResult of fnName: RTFnName * returnType: RTTypeReference

          | RecordField of
            recordTypeName: RTTypeName *
            fieldName: String *
            fieldType: RTTypeReference

          | DictKey of key: String * typ: RTTypeReference

          | EnumField of
            enumTypeName: RTTypeName *
            caseName: String *
            fieldIndex: Int64 *
            fieldCount: Int64 *
            fieldType: RTTypeReference

          | DBQueryVariable of varName: String * expected: RTTypeReference

          | DBSchemaType of name: String * expectedType: RTTypeReference

          | ListIndex of index: Int64 * listTyp: RTTypeReference * parent: Context

          | TupleIndex of
            index: Int64 *
            elementType: RTTypeReference *
            parent: Context

          | FnValResult of returnType: RTTypeReference


        type ErrorType =
          | ValueNotExpectedType of
            actualValue: RTDval *
            expectedType: RTTypeReference
          | TypeDoesntExist of RTTypeName

        type Error =
          { errorType: ErrorType
            context: Context }


        module Error =
          let valuePath (context: Context) : String =
            // match context with
            // | TCK.FunctionCallParameter(_, parameter, _) -> parameter.name
            // | TCK.FunctionCallResult(_, _) -> "result"
            // | TCK.RecordField(_, fieldName, _) -> fieldName
            // | TCK.DictKey(key, _) -> $".{key}"
            // | TCK.EnumField(_, caseName, _, _, _) -> caseName
            // | TCK.DBSchemaType(dbName, _) -> dbName
            // | TCK.DBQueryVariable(varName, _) -> varName
            // | TCK.ListIndex(index, _, parent) -> valuePath parent + $"[{index}]"
            // | TCK.TupleIndex(index, _, parent) -> valuePath parent + $"[{index}]"
            "RTETODO: valuePath"

          let rootContext (context: Context) : Context =
            match context with
            | FunctionCallParameter _ -> context
            | FunctionCallResult _ -> context
            | RecordField _ -> context
            | DictKey _ -> context
            | EnumField _ -> context
            | DBSchemaType _ -> context
            | DBQueryVariable _ -> context
            | ListIndex(_, _, parent) -> rootContext parent
            | TupleIndex(_, _, parent) -> rootContext parent
            | FnValResult _ -> context


          /// Return the segments describing the context as a short name, used in the description of errors
          let contextSummary (context: Context) : List<ErrorSegment.ErrorSegment> =
            match context with
            | FunctionCallParameter(fnName, parameter, paramIndex) ->
              [ ErrorSegment.ErrorSegment.FunctionName fnName
                ErrorSegment.ErrorSegment.String "'s "
                ErrorSegment.ErrorSegment.Ordinal(paramIndex + 1L)
                ErrorSegment.ErrorSegment.String " argument ("
                ErrorSegment.ErrorSegment.ParamName parameter.name
                ErrorSegment.ErrorSegment.String ")" ]

            | FunctionCallResult(fnName, returnType) ->
              [ ErrorSegment.ErrorSegment.FunctionName fnName
                ErrorSegment.ErrorSegment.String "'s return value" ]


            | RecordField(recordType, fieldName, _) ->
              [ ErrorSegment.ErrorSegment.TypeName recordType
                ErrorSegment.ErrorSegment.String "'s "
                ErrorSegment.ErrorSegment.FieldName fieldName
                ErrorSegment.ErrorSegment.String " field" ]

            // | DictKey(key, _) ->
            //   let typeName =
            //     FQName.BuiltIn { name = TypeName.TypeName "Dict"; modules = []; version = 0 }
            //   [ TypeName typeName; String "'s "; FieldName key; String " value" ]

            | EnumField(enumType, caseName, fieldIndex, _, _) ->
              [ ErrorSegment.ErrorSegment.TypeName enumType
                ErrorSegment.ErrorSegment.String "."
                ErrorSegment.ErrorSegment.InlineFieldName caseName
                ErrorSegment.ErrorSegment.String "'s "
                ErrorSegment.ErrorSegment.Ordinal(fieldIndex + 1L)
                ErrorSegment.ErrorSegment.String " argument" ]

            | DBSchemaType(dbName, expectedType) ->
              [ ErrorSegment.ErrorSegment.String "DB "
                ErrorSegment.ErrorSegment.DBName dbName
                ErrorSegment.ErrorSegment.String "'s value" ]

            | DBQueryVariable(varName, _) ->
              [ ErrorSegment.ErrorSegment.String "Variable "
                ErrorSegment.ErrorSegment.VarName varName ]

            | TupleIndex(index, typ, parent) ->
              let rootContext = rootContext parent

              [ String "In " ]
              @ contextSummary rootContext
              @ [ String ", the nested value "; VarName(valuePath context) ]

            | ListIndex(index, typ, parent) ->
              let rootContext = rootContext parent

              [ String "In " ]
              @ contextSummary rootContext
              @ [ String ", the nested value "; VarName(valuePath context) ]

            | FnValResult(_) ->
              [ ErrorSegment.ErrorSegment.String "Function return value" ]


          let rec contextAsActualExpected
            (argument: RuntimeTypes.Dval.Dval)
            (context: Context)
            : List<ErrorSegment.ErrorSegment> * List<ErrorSegment.ErrorSegment> =

            // RTETODO: We do actual and expected in the same function so that we can display
            // them the same way. This hasn't been ported for all Context types, but
            // should be.
            let defaultActual =
              [ ErrorSegment.ErrorSegment.IndefiniteArticle
                ErrorSegment.ErrorSegment.TypeOfValue argument
                ErrorSegment.ErrorSegment.String ": "
                ErrorSegment.ErrorSegment.FullValue argument ]

            match context with
            | FunctionCallParameter(fnName, parameter, paramIndex) ->
              let segments =
                [ ErrorSegment.ErrorSegment.String "("
                  ErrorSegment.ErrorSegment.InlineParamName parameter.name
                  ErrorSegment.ErrorSegment.String ": "
                  ErrorSegment.ErrorSegment.TypeReference parameter.typ
                  ErrorSegment.ErrorSegment.String ")" ]

              (defaultActual, segments)


            | FunctionCallResult(fnName, returnType) ->
              // format:
              // Option<String>
              let segment = [ ErrorSegment.ErrorSegment.TypeReference returnType ]
              (defaultActual, segment)


            | RecordField(recordType, fieldName, fieldType) ->
              // format:
              // ({ name : string; ... }) // some description

              // TODO: fetch the type and lookup the field definition as a comment
              let comment = []
              // let comment =
              //   if fieldDef.description = "" then
              //     []
              //   else
              //     [ String " // "; Description fieldDef.description ]

              let segment =
                Stdlib.List.append
                  [ ErrorSegment.ErrorSegment.String "({ "
                    ErrorSegment.ErrorSegment.InlineFieldName fieldName
                    ErrorSegment.ErrorSegment.String ": "
                    ErrorSegment.ErrorSegment.TypeReference fieldType
                    ErrorSegment.ErrorSegment.String "; ... })" ]
                  comment

              (defaultActual, segment)


            | DictKey(key, typ) ->
              // format:
              // ({ "name" : String; ... })
              let segment =
                [ ErrorSegment.ErrorSegment.String "({ "
                  ErrorSegment.ErrorSegment.InlineFieldName key
                  ErrorSegment.ErrorSegment.String ": "
                  ErrorSegment.ErrorSegment.TypeReference typ
                  ErrorSegment.ErrorSegment.String "; ... })" ]

              defaultActual, segment


            | EnumField(enumType, caseName, fieldIndex, fieldCount, fieldType) ->
              // format:
              //   Ok (..., string, ...) // some description
              // RTETODO: extract description from the type definition later

              let prefix =
                if fieldIndex == 0L then
                  []
                else
                  [ ErrorSegment.ErrorSegment.String "..., " ]

              let suffix =
                if fieldIndex == fieldCount - 1L then
                  []
                else
                  [ ErrorSegment.ErrorSegment.String ", ..." ]

              let openParen =
                if fieldCount > 0L then
                  [ ErrorSegment.ErrorSegment.String "(" ]
                else
                  []

              let closeParen =
                if fieldCount > 0L then
                  [ ErrorSegment.ErrorSegment.String ")" ]
                else
                  []

              //CLEANUP de-duplicate these 2 segments a bit
              let fieldTypeSegment =
                [ ErrorSegment.ErrorSegment.ShortTypeName enumType
                  ErrorSegment.ErrorSegment.String "."
                  ErrorSegment.ErrorSegment.InlineFieldName caseName
                  ErrorSegment.ErrorSegment.String " " ]
                |> Stdlib.List.append openParen
                |> Stdlib.List.append prefix
                |> Stdlib.List.append
                  [ ErrorSegment.ErrorSegment.TypeReference fieldType ]
                |> Stdlib.List.append suffix
                |> Stdlib.List.append closeParen

              let argumentSegment =
                [ ErrorSegment.ErrorSegment.ShortTypeName enumType
                  ErrorSegment.ErrorSegment.String "."
                  ErrorSegment.ErrorSegment.InlineFieldName caseName
                  ErrorSegment.ErrorSegment.String " " ]
                |> Stdlib.List.append openParen
                |> Stdlib.List.append prefix
                |> Stdlib.List.append
                  [ ErrorSegment.ErrorSegment.TypeOfValue argument ]
                |> Stdlib.List.append suffix
                |> Stdlib.List.append closeParen

              (argumentSegment, fieldTypeSegment)


            | DBSchemaType(dbName, expectedType) ->
              let segment = [ ErrorSegment.ErrorSegment.TypeReference expectedType ]
              (defaultActual, segment)


            | DBQueryVariable(varName, expected) ->
              let segment =
                [ ErrorSegment.ErrorSegment.String "("
                  ErrorSegment.ErrorSegment.InlineVarName varName
                  ErrorSegment.ErrorSegment.String ": "
                  ErrorSegment.ErrorSegment.TypeReference expected
                  ErrorSegment.ErrorSegment.String ")" ]

              (defaultActual, segment)

            | ListIndex(index, typ, parent) ->
              defaultActual, [ ErrorSegment.ErrorSegment.TypeReference typ ]

            | TupleIndex(index, typ, parent) ->
              defaultActual, [ ErrorSegment.ErrorSegment.TypeReference typ ]

            | FnValResult(returnType) ->
              defaultActual, [ ErrorSegment.ErrorSegment.TypeReference returnType ]


          let contextVerb (context: Context) : String =
            match context with
            | FunctionCallParameter _ -> "passed"
            | FunctionCallResult _ -> "returned"
            | RecordField _ -> "passed"
            | DictKey _ -> "passed"
            | EnumField _ -> "passed"
            | DBSchemaType _ -> "passed"
            | DBQueryVariable _ -> "passed"
            | ListIndex _ -> "passed"
            | TupleIndex _ -> "passed"
            | FnValResult _ -> "returned"



          let toSegments (e: Error) : REErrorOutput =
            match e.errorType with
            | ValueNotExpectedType(argument, expected) ->
              let summary =
                Stdlib.List.append
                  (contextSummary e.context)
                  [ ErrorSegment.ErrorSegment.String " should be "
                    ErrorSegment.ErrorSegment.IndefiniteArticle
                    ErrorSegment.ErrorSegment.TypeReference expected ]

              let extraExplanation =
                [ ErrorSegment.ErrorSegment.String ". However, "
                  ErrorSegment.ErrorSegment.IndefiniteArticle
                  ErrorSegment.ErrorSegment.TypeOfValue argument
                  ErrorSegment.ErrorSegment.String " ("
                  ErrorSegment.ErrorSegment.InlineValue argument
                  ErrorSegment.ErrorSegment.String ") was "
                  ErrorSegment.ErrorSegment.String(contextVerb e.context)
                  ErrorSegment.ErrorSegment.String " instead." ]

              let (actual, expected) = contextAsActualExpected argument e.context

              ErrorOutput
                { summary = summary
                  extraExplanation = extraExplanation
                  actual = actual
                  expected = expected }

            | _ ->
              ErrorOutput
                { summary =
                    [ ErrorSegment.ErrorSegment.String
                        "RTETODO typeChecker.toSegments" ]
                  extraExplanation = []
                  actual = []
                  expected = [] }