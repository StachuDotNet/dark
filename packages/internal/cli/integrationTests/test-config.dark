module Darklang =
  module Internal =
    module Cli =
      module IntegrationTests =
        module TestConfig =
          let testDir = "./packages/internal/cli/integrationTests/testResults"
          let projectRoot = "."

          let getExpectedDir () : String = $"{testDir}/expected"
          let getActualDir () : String = $"{testDir}/actual"
          let getFailureLogsDir () : String = $"{testDir}/failed-tests-logs"

          let getExpectedFilePath (expectedOutputFile: String): String =
            $"{getExpectedDir ()}/{expectedOutputFile}"

          let getActualFilePath (testName: String): String =
            let safeName = TestUtils.safeFileName testName
            $"{getActualDir ()}/{safeName}.txt"

          let defaultInteractiveTimeout = 20L

          let buildCliCommand (arguments: List<String>): String =
            let escapedArgs = 
              arguments
              |> Stdlib.List.map (fun arg ->
                // Escape arguments that contain shell special characters
                let hasBrackets = Stdlib.String.contains arg "[" || Stdlib.String.contains arg "]"
                let hasSemicolon = Stdlib.String.contains arg ";"
                let hasQuotes = Stdlib.String.contains arg "\""
                if hasBrackets || hasSemicolon then
                  $"\"{arg}\""
                else if hasQuotes then
                  $"'{arg}'"
                else
                  arg)
            let argsString = Stdlib.String.join escapedArgs " "
            $"./scripts/run-cli {argsString}"
