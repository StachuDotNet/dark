let listDirectoryRecursive (dir: String) : List<String> =
  let contents = Builtin.Directory.list dir

  let (files, dirs) =
    contents
    |> PACKAGE.Darklang.Stdlib.List.partition (fun x -> Builtin.File.isNormal x)

  let nested =
    dirs
    |> PACKAGE.Darklang.Stdlib.List.map (fun d -> listDirectoryRecursive d)
    |> Builtin.LocalExec.BuiltIns.List.flatten

  dirs
  |> PACKAGE.Darklang.Stdlib.List.append files
  |> PACKAGE.Darklang.Stdlib.List.append nested


// Packages
let listPackageFilesOnDisk (dir: String) : List<String> =
  dir
  |> listDirectoryRecursive
  |> PACKAGE.Darklang.Stdlib.List.filter (fun x ->
    x |> Builtin.LocalExec.BuiltIns.String.endsWith ".dark")




let saveItemToCanvas
  (json: String)
  (name: String)
  : PACKAGE.Darklang.Stdlib.Result.Result<Unit, String> =
  let response =
    PACKAGE.Darklang.Stdlib.HttpClient.request
      "POST"
      $"http://dark-packages.dlio.localhost:11003/{name}s"
      []
      (PACKAGE.Darklang.Stdlib.String.toBytes json)

  let errMsg = $"Error saving {name} to canvas"

  match response with
  | Ok response ->
    match response.statusCode with
    | 200L -> PACKAGE.Darklang.Stdlib.Result.Result.Ok()
    | _ ->
      Builtin.printLine $"Type: {name}; Json: {json}"

      Builtin.printLine
        $"Status: {PACKAGE.Darklang.Stdlib.Int64.toString response.statusCode}"

      Builtin.printLine
        $"Body: {response.body |> PACKAGE.Darklang.Stdlib.String.fromBytesWithReplacement}"

      Builtin.printLine errMsg
      PACKAGE.Darklang.Stdlib.Result.Result.Error errMsg
  | Error err ->
    Builtin.printLine $"Type: {name}; Json: {json}"
    Builtin.printLine err
    Builtin.printLine errMsg
    PACKAGE.Darklang.Stdlib.Result.Result.Error errMsg



let loadPackageFileIntoDarkCanvas
  (filename: String)
  : PACKAGE.Darklang.Stdlib.Result.Result<Unit, String> =
  let package =
    filename
    |> Builtin.File.read
    |> Builtin.unwrap
    |> PACKAGE.Darklang.Stdlib.String.fromBytesWithReplacement
    |> Builtin.LocalExec.Packages.parse filename
    |> Builtin.unwrap

  let fnResults =
    package.fns
    |> PACKAGE.Darklang.Stdlib.List.map (fun fn ->
      fn
      |> Builtin.Json.serialize<PACKAGE.Darklang.LanguageTools.ProgramTypes.PackageFn.PackageFn>
      |> saveItemToCanvas "function")

  let typeResults =
    package.types
    |> PACKAGE.Darklang.Stdlib.List.map (fun t ->
      t
      |> Builtin.Json.serialize<PACKAGE.Darklang.LanguageTools.ProgramTypes.PackageType>
      |> saveItemToCanvas "type")

  let constantResults =
    package.constants
    |> PACKAGE.Darklang.Stdlib.List.map (fun c ->
      c
      |> Builtin.Json.serialize<PACKAGE.Darklang.LanguageTools.ProgramTypes.PackageConstant>
      |> saveItemToCanvas "constant")

  // Flatten all the result lists into one list
  let allResults =
    [ fnResults; typeResults; constantResults ]
    |> PACKAGE.Darklang.Stdlib.List.flatten

  let firstError =
    PACKAGE.Darklang.Stdlib.List.findFirst allResults (fun r ->
      match r with
      | Error _ -> true
      | Ok _ -> false)

  // Find the first error in the flattened list
  match firstError with
  | Some(Error err) -> PACKAGE.Darklang.Stdlib.Result.Result.Error err
  | _ -> PACKAGE.Darklang.Stdlib.Result.Result.Ok()


let printPackageFunction
  (p: PACKAGE.Darklang.LocalExec.Packages.FunctionName)
  : Unit =
  let modules = p.modules |> PACKAGE.Darklang.Stdlib.String.join "."

  Builtin.printLine
    $"Package Function {p.owner}.{modules}.{p.name}_v{PACKAGE.Darklang.Stdlib.Int64.toString_v0 p.version}"

let printPackageType (p: PACKAGE.Darklang.LocalExec.Packages.TypeName) : Unit =
  let modules = p.modules |> PACKAGE.Darklang.Stdlib.String.join "."

  Builtin.printLine
    $"Package Type {p.owner}.{modules}.{p.name}_v{PACKAGE.Darklang.Stdlib.Int64.toString_v0 p.version}"

let printPackageConstant
  (p: PACKAGE.Darklang.LocalExec.Packages.ConstantName)
  : Unit =
  let modules = p.modules |> PACKAGE.Darklang.Stdlib.String.join "."

  Builtin.printLine
    $"Package Constant {p.owner}.{modules}.{p.name}_v{PACKAGE.Darklang.Stdlib.Int64.toString_v0 p.version}"

let printAllPackagesInDb () : Unit =
  let functions = Builtin.LocalExec.Packages.listFunctions ()

  functions
  |> Builtin.LocalExec.BuiltIns.List.iter (fun p -> printPackageFunction p)

  let types = Builtin.LocalExec.Packages.listTypes ()
  types |> Builtin.LocalExec.BuiltIns.List.iter (fun p -> printPackageType p)

  let constants = Builtin.LocalExec.Packages.listConstants ()

  constants
  |> Builtin.LocalExec.BuiltIns.List.iter (fun p -> printPackageConstant p)


// parse args and execute
type CliOptions =
  | Help
  | LoadPackagesIntoDarkCanvas
  | ListPackages
  | BadOption of String

let usage () : String =
  "Usage: LocalExec [options]

    Options:
      -h, --help          Show this help message and exit
      load-packages-dark  Load packages from disk into dark packages canvas
      list-packages       List packages
  "


let parseArgs (args: List<String>) : CliOptions =
  match args with
  | [] -> CliOptions.Help
  | [ "-h" ] -> CliOptions.Help
  | [ "--help" ] -> CliOptions.Help

  | [ "list-packages" ] -> CliOptions.ListPackages
  | [ "load-packages" ] ->
    CliOptions.BadOption
      "`load-packages` now executes in F# (not sure how you got here)"
  | [ "load-packages-dark" ] -> CliOptions.LoadPackagesIntoDarkCanvas

  | _ -> CliOptions.BadOption "Invalid arguments"



let main (args: List<String>) : Int64 =
  match parseArgs args with
  | Help ->
    Builtin.printLine (usage ())
    0L

  | ListPackages ->
    printAllPackagesInDb ()
    0L

  | LoadPackagesIntoDarkCanvas ->
    let files = listPackageFilesOnDisk "/home/dark/app/packages"

    // TODO: clear packages from dark canvas first?

    // Wait for canvas to be ready
    let available =
      PACKAGE.Darklang.Stdlib.List.fold
        (PACKAGE.Darklang.Stdlib.List.range 0L 10L)
        (PACKAGE.Darklang.Stdlib.Result.Result.Error "Not started")
        (fun found num ->
          match found with
          | Ok() -> PACKAGE.Darklang.Stdlib.Result.Result.Ok()
          | Error _ ->
            (PACKAGE.Darklang.Stdlib.HttpClient.request
              "GET"
              "http://dark-packages.dlio.localhost:11003/health"
              []
              Builtin.Bytes.empty)
            |> PACKAGE.Darklang.Stdlib.Result.map (fun _ -> ())
            |> PACKAGE.Darklang.Stdlib.Result.mapError (fun err ->
              let errMsg = PACKAGE.Darklang.Stdlib.HttpClient.toString err
              Builtin.printLine $"Error: {errMsg}"
              Builtin.Time.sleep 1000.0
              err))


    match available with
    | Error err ->
      let errMsg = PACKAGE.Darklang.Stdlib.HttpClient.toString err
      Builtin.printLine $"Error waiting for dark packages canvas: {errMsg}"
      1L
    | Ok() ->
      Builtin.printLine "Dark packages canvas is ready"

      let result =
        PACKAGE.Darklang.Stdlib.List.fold
          files
          (PACKAGE.Darklang.Stdlib.Result.Result.Ok())
          (fun acc f ->
            Builtin.printLine $"Loading {f}"

            match acc with
            | Error _err -> acc
            | Ok() ->
              match loadPackageFileIntoDarkCanvas f with
              | Error err ->
                PACKAGE.Darklang.Stdlib.Result.Result.Error(
                  $"Failed to load packages from {f}:\n" ++ err
                )
              | Ok() -> PACKAGE.Darklang.Stdlib.Result.Result.Ok())

      match result with
      | Error err ->
        Builtin.printLine err
        1L
      | Ok() ->
        Builtin.printLine "Done loading packages from disk"
        printAllPackagesInDb ()
        0L

  | BadOption msg ->
    Builtin.printLine ("Error: " ++ msg)
    1L

main args